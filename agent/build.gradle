/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

configurations.all {
    // this is only needed while using instrumentation SNAPSHOTS
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    // this is only needed while using instrumentation SNAPSHOTS
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

apply from: "$buildScriptsDir/common-java.gradle"


def instrumentationVersion = '0.5.0-SNAPSHOT'

def shadowPrefix = 'com.microsoft.applicationinsights.agent.shadow'
def corePrefix = 'com.microsoft.applicationinsights.core.dependencies' // some dependencies are already shaded in core

shadowJar() {

    archiveClassifier = 'intermediate'
    mergeServiceFiles()

    dependencies {
        exclude(dependency('com.google.guava:guava')) // already brought in via core
        exclude(dependency('com.github.trask:checker-framework-annotations-for-jdk6')) // already brought in via core
        exclude(dependency {
            it.moduleGroup == 'org.glowroot.instrumentation' &&
                    it.moduleName != 'instrumentation-api' &&
                    it.moduleName != 'instrumentation-engine'
        })
    }

    relocate('org.objectweb.asm', "${shadowPrefix}.org.objectweb.asm")
    relocate('org.slf4j', "${shadowPrefix}.org.slf4j")
    relocate('ch.qos.logback', "${shadowPrefix}.ch.qos.logback")

    // this shades internal use of java util logging which is needed to prevent java util logging from being initialized
    // early (e.g. in the case of jboss which wants to do some setup before jul is initialized)
    relocate('java.util.logging', 'org.glowroot.instrumentation.engine.jul')

    // core
    relocate 'org.apache.commons.logging', "${corePrefix}.apachecommons.logging" // needed for jcl-over-slf4j
    relocate 'com.google', "${corePrefix}.google"
    relocate 'org.checkerframework', "${corePrefix}.checkerframework"
    relocate 'javax.annotation', "${corePrefix}.javaxannotation"
    relocate 'org.codehaus', "${corePrefix}.codehaus"

    relocate('logback.configurationFile', 'ai.logback.configurationFile')
    relocate('logback.xml', 'ai.logback.xml')
    relocate('logback.groovy', 'ai.logback.groovy') // to prevent accidentally picking up from user's class path
    relocate('logback-test.xml', 'ai.logback-test.xml') // to prevent accidentally picking up from user's class path

    relocate('commons-logging.properties', 'ai.commons-logging.properties') // to prevent accidentally picking up from user's class path

    relocate('glowroot.debug', 'ai.debug') // system property names

    exclude 'META-INF/maven/**'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE*'
    exclude 'META-INF/services/javax.servlet.ServletContainerInitializer'
    exclude 'mozilla/**'
    exclude 'commons-logging.properties'
}

task shadowJar2(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {

    dependsOn shadowJar

    from {
        zipTree(shadowJar.archiveFile)
    }

    configurations = [project.configurations.runtime]

    archiveClassifier = ''
    mergeServiceFiles()

    dependencies {
        include(dependency {
            it.moduleGroup == 'org.glowroot.instrumentation' &&
                    it.moduleName != 'instrumentation-api' &&
                    it.moduleName != 'instrumentation-engine'
        })
    }

    transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
        resource = 'META-INF/instrumentation.list'
    }

    manifest {
        attributes("Premain-Class": "com.microsoft.applicationinsights.agent.internal.Premain",
                "Can-Redefine-Classes": "true",
                "Can-Retransform-Classes": "true",
                "agent-sdk-version": project.version
        )
    }
}

archivesBaseName = 'applicationinsights-agent'

jar {
    dependsOn shadowJar2
    enabled = false
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'org.slf4j:jcl-over-slf4j:1.7.26' // this is needed to pick up logging from apache http client
    compile 'ch.qos.logback:logback-classic:1.2.3'

    compile "org.glowroot.instrumentation:instrumentation-engine:$instrumentationVersion"
    compile "com.google.guava:guava:27.1-android"
    compile "org.glowroot.instrumentation:instrumentation-api:$instrumentationVersion"
    runtime "org.glowroot.instrumentation:instrumentation-executor:$instrumentationVersion"
    runtime "org.glowroot.instrumentation:instrumentation-servlet:$instrumentationVersion"
    runtime "org.glowroot.instrumentation:instrumentation-spring:$instrumentationVersion" // need for operation name
    runtime "org.glowroot.instrumentation:instrumentation-jdbc:$instrumentationVersion"
    runtime "org.glowroot.instrumentation:instrumentation-redis:$instrumentationVersion"
    runtime "org.glowroot.instrumentation:instrumentation-apache-http-client:$instrumentationVersion"
    runtime "org.glowroot.instrumentation:instrumentation-okhttp:$instrumentationVersion"
    runtime "org.glowroot.instrumentation:instrumentation-log4j:$instrumentationVersion"
    runtime "org.glowroot.instrumentation:instrumentation-logback:$instrumentationVersion"

    compile(project(path: ':core', configuration: 'shadow'))
}
