apply from: "$rootDir/gradle/instrumentation.gradle"

muzzle {
  pass {
    group = 'org.apache.logging.log4j'
    module = 'log4j-core'
    versions = '(,)'
  }

  pass {
    group = 'org.apache.logging.log4j'
    module = 'log4j-api'
    versions = '(,)'
  }
}

configurations {
  // In order to test the real log4j library we need to remove the log4j transitive
  // dependency 'log4j-over-slf4j' brought in by :testing-common which would shadow
  // the log4j module under test using a proxy to slf4j instead.
  testImplementation.exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
}

dependencies {
  library group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
  library group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.0'

  testImplementation group: 'com.lmax', name: 'disruptor', version: '3.4.2'
}

test.finalizedBy(tasks.register('testAsync', Test) {
  jvmArgs '-DLog4j2.contextSelector=org.apache.logging.log4j.core.async.AsyncLoggerContextSelector'
})
