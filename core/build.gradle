/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

apply from: "$buildScriptsDir/common-java.gradle"

def sdkVersionFileDir = "$project.buildDir/src/generated/main/resources"

sourceSets {
    main {
        resources {
            srcDir sdkVersionFileDir
        }
    }
}

import com.microsoft.applicationinsights.build.tasks.PropsFileGen

archivesBaseName = 'applicationinsights-core'

repositories {
    // maven local is needed for locally built otel fork
    mavenLocal()
}

configurations {
    forTheDLL { transitive = false }
}

dependencies {
    implementation project(":agent:agent-profiler:agent-profiler-api")
    implementation project(":agent:agent-profiler:agent-alerting-api")
    implementation project(":agent:agent-profiler:agent-alerting")
    implementation project(":agent:agent-gc-monitor:gc-monitor-api")
    implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang
    implementation group: 'org.apache.commons', name: 'commons-text', version: versions.commonsText
    implementation group: 'com.squareup.moshi', name: 'moshi', version: versions.moshi
    implementation group: 'com.google.code.gson', name: 'gson', version: versions.gson
    implementation group: 'com.github.oshi', name:'oshi-core', version: versions.oshi
    implementation group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4j

    implementation group: 'com.azure', name: 'azure-monitor-opentelemetry-exporter', version: '1.0.0-beta.4'
    implementation(group: 'com.azure', name: 'azure-identity', version: '1.2.4') {
        // TODO (kryalama) is this ok to exclude?
        //  "This dependency can be excluded if IntelliJ Credential is not being used for authentication
        //   via `IntelliJCredential` or `DefaultAzureCredential`"
        //  NOTE this exclusion saves 6.5 mb !!!!
        exclude group: 'org.linguafranca.pwdb', module: 'KeePassJava2'
    }
    implementation group: 'com.azure', name: 'azure-core', version: versions.azureCore

    implementation group: 'io.opentelemetry', name: 'opentelemetry-api', version: versions.opentelemetry
    implementation group: 'org.jctools', name: 'jctools-core', version: '3.3.0'

    implementation "org.checkerframework:checker-qual:3.14.0"

    // to use the cache from opentelemetry javaagent
    implementation group: 'io.opentelemetry.instrumentation', name: 'opentelemetry-instrumentation-api-caching', version: versions.opentelemetryInstrumentationAlpha

    testImplementation group: 'junit', name: 'junit', version: versions.junit
    testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version: versions.hamcrest
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: versions.hamcrest
    testImplementation group: 'org.mockito', name: 'mockito-core', version: versions.mockito
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: versions.systemRules
    testImplementation group: 'com.google.code.gson', name: 'gson', version: versions.gson
    testImplementation group: 'io.github.hakky54', name: 'logcaptor', version: versions.logcaptor

    testImplementation project(":agent:agent-profiler:agent-service-profiler")
    testImplementation group: 'com.microsoft.jfr', name: 'jfr-streaming', version: versions.microsoftJfrStreaming
    testImplementation group: 'com.azure', name: 'azure-storage-blob', version: versions.azureStorageBlob

    forTheDLL 'com.microsoft.azure:applicationinsights-core:2.6.2'
}

task copyTheDLL(type: Copy) {
    from zipTree(configurations.forTheDLL.singleFile).matching {
        include '*.dll'
    }
    into "$buildDir/resources/main"
}

processResources.dependsOn copyTheDLL

task generateVersionProperties(type: PropsFileGen) {
    targetFile = new File(sdkVersionFileDir, "sdk-version.properties")
    property "version", project.version
}

processResources.dependsOn generateVersionProperties
