/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */
plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply from: "$buildScriptsDir/common-java.gradle"
apply from: "$buildScriptsDir/publishing.gradle"
if (System.env."windir" != null) {
    apply from: "native.gradle"
} else {
    logger.warn("Native binaries build is only supported on Windows systems; native components will not be built.")
}

import com.microsoft.applicationinsights.build.tasks.PropsFileGen

archivesBaseName = 'applicationinsights-core'

dependencies {
    provided (project(':agent')) { transitive = false }
    compile ([group: 'eu.infomas', name: 'annotation-detector', version: '3.0.5'])
    compile ([group: 'commons-io', name: 'commons-io', version: '2.5' ])
    compile ([group: 'org.apache.commons', name: 'commons-lang3', version: '3.6']) 
    compile ([group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'])
    compile ([group: 'com.google.guava', name: 'guava', version: '22.0'])
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.8.0'
    testCompile group: 'com.google.code.gson', name: 'gson', version: '1.7.2'
}

shadowJar {
    classifier=''
    relocate 'org.apache.http', 'com.microsoft.applicationinsights.core.dependencies.http'
    relocate 'eu.infomas.annotation', 'com.microsoft.applicationinsights.core.dependencies.annotation'
    relocate 'org.apache.commons', 'com.microsoft.applicationinsights.core.dependencies.apachecommons'
    relocate 'com.google.common', 'com.microsoft.applicationinsights.core.dependencies.googlecommon'
    relocate 'javax.annotation', 'com.microsoft.applicationinsights.core.dependencies.javaxannotation'
}

jar {
    dependsOn shadowJar
    enabled = false
}

def sdkVersionFileDir = "$project.buildDir/src/generated/main/resources"
task generateVersionProperties(type: PropsFileGen) {
    targetFile = new File(sdkVersionFileDir, "sdk-version.properties")
    property "version", project.version
}

processResources.dependsOn generateVersionProperties

sourceSets {
    main {
        resources {
            srcDir sdkVersionFileDir
        }
    }
}

// region Publishing properties

projectPomName = project.msftAppInsights + " Java SDK Core"
projectPomDescription = "This is the core module of " + project.msftAppInsightsJavaSdk

whenPomConfigured = { p ->
    def agentArtifactId = project(":agent").jar.baseName
    p.dependencies = p.dependencies.findAll { dep -> dep.artifactId != agentArtifactId }
    writePomToArtifactsDirectory(p, project.name)
}

// endregion Publishing properties
