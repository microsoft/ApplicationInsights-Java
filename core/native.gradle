/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

apply plugin: 'cpp'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'commons-io', name: 'commons-io', version: '2.6'
    }
}

import org.apache.commons.io.FilenameUtils

def javaIncludeDir = new File(System.env.'JAVA_HOME', "include")
def programFilesX86 = System.env.'ProgramFiles(x86)' ?: 'C:\\Program Files (x86)'

def winSdkDir = "$programFilesX86\\Windows Kits\\8.1"
if (System.env.APPINSIGHTS_WIN_SDK_PATH) {
    winSdkDir = System.env.APPINSIGHTS_WIN_SDK_PATH
    logger.info "Using custom Windows SDK path: $winSdkDir"
} else {
    logger.info "Using default Windows SDK path: $winSdkDir"
}
def vsToolsDir = "$programFilesX86\\Microsoft Visual Studio 14.0"
if (System.env.APPINSIGHTS_VS_PATH) {
    vsToolsDir = System.env.APPINSIGHTS_VS_PATH
    logger.info "Using custom Visual Studio Tools path: $vsToolsDir"
}

idea {
    module {
        sourceDirs += file("$projectDir/src/windows/cpp")
    }
}

// region Native binary definition and configuration
model {
    toolChains {
        visualCpp(VisualCpp) {
            logger.info "Setting custom Windows SDK path: $winSdkDir"
            windowsSdkDir winSdkDir
            if (System.env.APPINSIGHTS_VS_PATH) {
                logger.info "Setting custom VS Build Tools path: $vsToolsDir"
                installDir vsToolsDir
            }
        }
    }
    buildTypes {
        release
        debug
    }
    platforms {
        x86 { architecture "x86" }
        x64 { architecture "amd64" }
    }

    components {
        "windows"(NativeLibrarySpec) {
            targetPlatform "x86"
            targetPlatform "x64"

            baseName = "applicationinsights-core-native-win"

            sources {
                cpp {
                    source {
                        include "**/*.cpp"
                        include "**/*.h"
                    }
                }
            }

            binaries.all {
                // Only build shared libraries
                if (!(delegate instanceof SharedLibraryBinarySpec)) {
                    buildable = false
                    return
                }

                sharedLibraryFile = ReconstructFileName(sharedLibraryFile, targetPlatform)
                if ((project.properties["isRelease"] ?: "false").toBoolean() == (buildType.name == "release")) {
                    logger.info "Adding \"$sharedLibraryFile.parentFile\" to sourceSets.main.resources"
                    sourceSets.main.resources.srcDir sharedLibraryFile.parentFile

                    if ((project.properties["core.native.artifacts.prebuilt"] ?: "false").toBoolean()) {
                        buildable = false
                        return
                    }

                    tasks.all { t ->
                        logger.info "Adding \"processResources\".dependsOn \"$t\" (${t.getClass()})"
                        processResources.dependsOn t
                    }
                }
                if (toolChain in VisualCpp) {
                    // Visual Studio Compiler Options: https://msdn.microsoft.com/en-us/library/fwkeyyhe.aspx
                    // Visual Studio Linker Options:   https://msdn.microsoft.com/en-us/library/y0zzbyt4.aspx

                    // Searches a directory for include files.
                    cppCompiler.args "/I$javaIncludeDir"
                    cppCompiler.args "/I$javaIncludeDir\\win32"

                    // Produces an output file to run on the common language runtime.
                    cppCompiler.args "/clr"

                    // Sets warning level.
                    cppCompiler.args "/W3"

                    // Catch both asynchronous (structured) and synchronous (C++) exceptions.
                    cppCompiler.args "/EHa"

                    // Compiler macros definition
                    cppCompiler.define "WIN32"
                    cppCompiler.define "UNICODE"
                    cppCompiler.define "_UNICODE"
                    cppCompiler.define "_WINDLL"

                    if (buildType == buildTypes.release) {
                        cppCompiler.define 'NDEBUG'

                        // Use the multithread-specific and DLL-specific version of the run-time library.
                        cppCompiler.args '/MD'

                        // Creates the fastest code in the majority of cases.
                        cppCompiler.args '/O2'
                    } else {
                        cppCompiler.define '_DEBUG'

                        // Generates complete debugging information.
                        cppCompiler.args '/Zi'

                        // Creates debugging information.
                        linker.args '/DEBUG'

                        //Disables optimization.
                        cppCompiler.args "/Od"

                        // Use the debug multithread-specific and DLL-specific version of the run-time library.
                        cppCompiler.args '/MDd'

                        cppCompiler.args '/FS'
                    }
                }
            }

        }
    }
}

sourceSets {
    main {
        resources {
            exclude "**/*.exp"
            exclude "**/*.lib"
            exclude "**/*.pdb"
        }
    }
}

tasks.register('windowsSharedLibrary') {
    dependsOn windowsX64DebugSharedLibrary, windowsX86DebugSharedLibrary, windowsX64ReleaseSharedLibrary, windowsX86ReleaseSharedLibrary
}

// This function will reconstruct the file name based on the buildType and targetPlatform
// New file name structure: <baseName><32|64>.<ext>
def ReconstructFileName(File originalFile, NativePlatform targetPlatform) {
    def originalFileName = originalFile.absolutePath
    def filePath = FilenameUtils.getFullPath(originalFileName)
    def baseName = FilenameUtils.getBaseName(originalFileName)
    def extension = FilenameUtils.getExtension(originalFileName)

    def bitnessPart = ""
    switch (targetPlatform.name) {
        case "x86" :
            bitnessPart = "32"
            break;
        case "x64" :
            bitnessPart = "64"
            break;
        default:
            logger.warn "Unexpected targetPlatform encountered - skipping setting the file name bitness part"
    }

    def newName = "$baseName$bitnessPart.$extension"
    def newFile = new File(filePath, newName)
    logger.debug "Reconstructed file name: $originalFile => $newFile"
    newFile
}

// endregion Private Methods
