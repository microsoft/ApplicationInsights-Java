/*
 * ApplicationInsights-Java
 * Copyright (c) Microsoft Corporation
 * All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy of this
 * software and associated documentation files (the ""Software""), to deal in the Software
 * without restriction, including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software, and to permit
 * persons to whom the Software is furnished to do so, subject to the following conditions:
 * The above copyright notice and this permission notice shall be included in all copies or
 * substantial portions of the Software.
 * THE SOFTWARE IS PROVIDED *AS IS*, WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED,
 * INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
 * PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE
 * FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR
 * OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
 * DEALINGS IN THE SOFTWARE.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}

apply from: "$buildScriptsDir/common-java.gradle"
apply from: "$buildScriptsDir/publishing.gradle"

archivesBaseName = 'applicationinsights-web'

dependencies {
    provided (project(':agent')) { transitive = false }
    compile (project(':core')) { transitive = false }
    compile ([group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'])
    compile ([group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'])
    compile ([group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.3'])
    provided 'com.opensymphony:xwork:2.0.4' // Struts 2
    provided 'org.springframework:spring-webmvc:3.1.0.RELEASE'
    provided group: 'javax.servlet', name: 'javax.servlet-api', version: '3.0.1'
    provided group: 'javax.enterprise', name: 'cdi-api', version: '1.1' // Java EE
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.8.0'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '7.0.0.M0'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '7.0.0.M0'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: '7.0.0.M0'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'
    testCompile group: 'org.json', name:'json', version:'20090211'
    testCompile group: 'com.microsoft.azure', name: 'azure-storage', version: '2.1.0'
}

shadowJar {
    classifier=''
    relocate 'org.apache.http', 'com.microsoft.applicationinsights.web.dependencies.http'
    relocate 'org.apache.commons', 'com.microsoft.applicationinsights.web.dependencies.apachecommons'
}

jar {
    dependsOn shadowJar
    enabled = false
}

// region Publishing properties

projectPomName = project.msftAppInsights + " Java SDK Web Module"
projectPomDescription = "This is the web module of " + project.msftAppInsightsJavaSdk

whenPomConfigured = { p ->
    def agentArtifactId = project(":agent").jar.baseName
    p.dependencies = p.dependencies.findAll { dep ->
        dep.artifactId != agentArtifactId &&
                !(dep.groupId in ['org.apache.http', 'org.apache.commons', 'commons-io',
                                  'org.apache.httpcomponents'])
    }
}

// endregion Publishing properties
